group 'org.utbot'

apply plugin: 'java-library'

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilerExecutionStrategy

tasks.withType(KotlinCompile)
        .configureEach {
            compilerExecutionStrategy.set(KotlinCompilerExecutionStrategy.IN_PROCESS)
        }

if (project.hasProperty('semVer')) {
    project.version = project.semVer
} else {
    project.version = '1.0-SNAPSHOT'
}

//noinspection GroovyAssignabilityCheck
buildscript {
    //noinspection GroovyAssignabilityCheck
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlin_version
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-allopen', version: kotlin_version
    }
}

subprojects {
    group = rootProject.group
    version = rootProject.version

    apply plugin: 'base'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    publishing {
        publications {
            //noinspection GroovyAssignabilityCheck
            jar(MavenPublication) {
                from components.java
                groupId 'org.utbot'
                artifactId project.name
            }
        }
    }

    //noinspection GroovyAssignabilityCheck
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

configure([
        project(':utbot-api'),
        project(':utbot-core'),
        project(':utbot-framework'),
        project(':utbot-framework-api'),
        project(':utbot-fuzzers'),
        project(':utbot-instrumentation'),
        project(':utbot-summary')
]) {
    publishing {
        //noinspection GroovyAssignabilityCheck
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/UnitTestBot/UTBotJava"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
