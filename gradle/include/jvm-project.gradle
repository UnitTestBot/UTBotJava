apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.cqfn.diktat.diktat-gradle-plugin'

dependencies {
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: coroutines_version
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-collections-immutable-jvm', version: collections_version
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version

    testImplementation("org.junit.jupiter:junit-jupiter"){
        version {
            strictly junit5_version
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += ["-Xallow-result-return-type", "-Xsam-conversions=class"]
        allWarningsAsErrors = false
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += ["-Xallow-result-return-type", "-Xsam-conversions=class"]
        allWarningsAsErrors = false
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava {
    options.compilerArgs << '-Xlint:all'
    options.encoding = 'UTF-8'
}

compileTestJava {
//    options.compilerArgs << '-Werror' << '-Xlint:all'
//    options.encoding = 'UTF-8'
}

test {
    // set heap size for the test JVM(s)
    minHeapSize = "128m"
    maxHeapSize = "3072m"

    jvmArgs '-XX:MaxHeapSize=3072m'

    useJUnitPlatform() {
        excludeTags 'slow', 'IntegrationTest'
    }

    afterTest { descriptor, result ->
        println "[$descriptor.classDisplayName] [$descriptor.displayName]: $result.resultType"
    }

    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Test summary: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

diktat {
    inputs {
        it.include("src/**/*.kt", "*.kts", "src/**/*.kts")
        it.exclude("build/**")
    }

    def sarifOutput = Boolean.parseBoolean(project.findProperty("diktat.githubActions"))

    /*
     * One of: "html", "json", "plain" (default), "sarif".
     *
     * Run "gradlew --continue diktatCheck -Pdiktat.githubActions=true"
     * to get a SARIF output.
     */
    reporter = sarifOutput ? "sarif" : "plain"

    /*
     * If `output` is empty, reports will be written to stdout.
     */
    output = sarifOutput ? "build/reports/diktat/diktat.sarif" : ""

    /*
     * You can configure `reporter` and `output` independently, or you can
     * set the `githubActions` flag which, if `true`, will do exactly the
     * same as above.
     */
    githubActions = false

    debug = false
}
