apply from: "${parent.projectDir}/gradle/include/jvm-project.gradle"

configurations {
    mlmodels
}

def osName = System.getProperty('os.name').toLowerCase().split()[0]
if (osName == "mac") osName = "macosx"
String classifier = osName + "-x86_64"

evaluationDependsOn(':utbot-framework')
compileTestJava.dependsOn tasks.getByPath(':utbot-framework:testClasses')

dependencies {
    implementation(project(":utbot-api"))
    implementation(project(":utbot-core"))
    implementation(project(":utbot-summary"))
    implementation(project(":utbot-framework-api"))
    implementation(project(":utbot-fuzzers"))
    implementation(project(":utbot-instrumentation"))
    implementation(project(":utbot-framework"))
    testImplementation project(':utbot-sample')
    testImplementation group: 'junit', name: 'junit', version: junit4_version

    implementation "com.github.UnitTestBot:soot:${soot_commit_hash}"
    implementation group: 'com.github.haifengl', name: 'smile-kotlin', version: '2.6.0'
    implementation group: 'com.github.haifengl', name: 'smile-plot', version: '2.6.0'
    implementation group: 'com.github.haifengl', name: 'smile-core', version: '2.6.0'
    implementation group: 'com.github.haifengl', name: 'smile-interpolation', version: '2.6.0'
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlin_logging_version
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    implementation group: 'org.bytedeco', name: 'arpack-ng', version: "3.7.0-1.5.4", classifier: "$classifier"
    implementation group: 'org.bytedeco', name: 'openblas', version: "0.3.10-1.5.4", classifier: "$classifier"
    implementation group: 'org.bytedeco', name: 'javacpp', version: javacpp_version, classifier: "$classifier"

    implementation group: 'tech.tablesaw', name: 'tablesaw-core', version: '0.38.2'
    implementation group: 'tech.tablesaw', name: 'tablesaw-jsplot', version: '0.38.2'

    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'

    implementation group: 'com.github.javaparser', name: 'javaparser-core', version: '3.22.1'

    implementation group: 'org.jsoup', name: 'jsoup', version: jsoup_version

    implementation "ai.djl:api:$djl_api_version"
    implementation "ai.djl.pytorch:pytorch-engine:$djl_api_version"
    implementation "ai.djl.pytorch:pytorch-native-auto:$pytorch_native_version"

    testImplementation project(':utbot-framework').sourceSets.test.output
}

test {

    minHeapSize = "128m"
    maxHeapSize = "3072m"

    jvmArgs '-XX:MaxHeapSize=3072m'

    useJUnitPlatform() {
        excludeTags 'slow', 'IntegrationTest'
    }
}

processResources {
    configurations.mlmodels.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        from(zipTree(artifact.getFile())) {
            into "models"
        }
    }
}

jar {
    dependsOn classes
    manifest {
        attributes 'Main-Class': 'org.utbot.QualityAnalysisKt'
    }

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    zip64 = true
}