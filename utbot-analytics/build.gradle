configurations {
    mlmodels
}

def osName = System.getProperty('os.name').toLowerCase().split()[0]
if (osName == "mac") osName = "macosx"
String classifier = osName + "-x86_64"

evaluationDependsOn(':utbot-framework')
compileTestJava.dependsOn tasks.getByPath(':utbot-framework:testClasses')

dependencies {
    api project(":utbot-framework")
    testImplementation project(':utbot-sample')
    testImplementation group: 'junit', name: 'junit', version: junit4Version

    implementation("org.unittestbot.soot:soot-utbot-fork:${sootVersion}") {
        exclude group:'com.google.guava', module:'guava'
    }
    implementation group: 'com.github.haifengl', name: 'smile-kotlin', version: '2.6.0'
    implementation group: 'com.github.haifengl', name: 'smile-plot', version: '2.6.0'
    implementation group: 'com.github.haifengl', name: 'smile-core', version: '2.6.0'
    implementation group: 'com.github.haifengl', name: 'smile-interpolation', version: '2.6.0'
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlinLoggingVersion
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    implementation group: 'org.bytedeco', name: 'arpack-ng', version: arpackNgVersion, classifier: "$classifier"
    implementation group: 'org.bytedeco', name: 'openblas', version: openblasVersion, classifier: "$classifier"

    implementation group: 'tech.tablesaw', name: 'tablesaw-core', version: '0.38.2'
    implementation group: 'tech.tablesaw', name: 'tablesaw-jsplot', version: '0.38.2'

    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    implementation group: 'com.github.javaparser', name: 'javaparser-core', version: '3.22.1'

    testImplementation project(':utbot-testing')
    testImplementation project(':utbot-framework').sourceSets.test.output
}

processResources {
    configurations.mlmodels.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        from(zipTree(artifact.getFile())) {
            into "models"
        }
    }
}

// TODO if you need utbot-analytics fat jar, use shadow jar to create a SEPARATE task for a fat jar.
//  Do not use main jar for a fat jar, because it breaks Gradle conflict resolution, here's how:
//    1. utbot-analytics depends on library A version 1.0 (and adds it to own main jar)
//    2. utbot-junit-contest depends on utbot-analytics and library A version 1.1
//    3. Both library A version 1.0 and version 1.1 end up on the classpath and it's a matter of chance which one is earlier
//  If utbot-analytics were to only declare its dependency on library A version 1.0 and not force it by adding it to a
//  main jar, then Gradle would be able to recognize the conflict of library A version 1.0 and version 1.1 and resolve
//  it according to a conflict resolution strategy, which by default picks the latest version, which works in most cases.
//  But if you put library A version 1.0 into some fat jar, Gradle will no longer be able to exclude it from the fat jar
//  in favor of a newer version when it needs to resolve dependency conflicts.
//jar {
//    dependsOn classes
//    manifest {
//        attributes 'Main-Class': 'org.utbot.QualityAnalysisKt'
//    }
//
//    dependsOn configurations.runtimeClasspath
//    from {
//        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
//    }
//
//    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
//    zip64 = true
//}