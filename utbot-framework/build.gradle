plugins {
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.7.20'
}

configurations {
    fetchSpringAnalyzerJar
    fetchInstrumentationJar
}

dependencies {

    api project(':utbot-java-fuzzing')
    api project(':utbot-instrumentation')
    api project(':utbot-summary')
    api project(':utbot-framework-api')
    api project(':utbot-rd')

    implementation group: 'com.jetbrains.rd', name: 'rd-framework', version: rdVersion
    implementation group: 'com.jetbrains.rd', name: 'rd-core', version: rdVersion

    implementation("org.unittestbot.soot:soot-utbot-fork:${sootVersion}") {
        exclude group:'com.google.guava', module:'guava'
    }
    implementation group: 'com.google.guava', name: 'guava', version: guavaVersion
    implementation group: 'com.esotericsoftware.kryo', name: 'kryo5', version: kryoVersion
    // this is necessary for serialization of some collections
    implementation group: 'de.javakaffee', name: 'kryo-serializers', version: kryoSerializersVersion

    implementation group: 'com.charleskorn.kaml', name: 'kaml', version: kamlVersion
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: jacksonVersion
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-cbor', version: kotlinxSerializationVersion
    implementation group: 'com.github.curious-odd-man', name: 'rgxgen', version: rgxgenVersion
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j2Version
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlinLoggingVersion
    implementation group: 'org.jacoco', name: 'org.jacoco.report', version: jacocoVersion
    implementation group: 'org.apache.commons', name: 'commons-text', version: apacheCommonsTextVersion
    // we need this for construction mocks from composite models
    implementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion

    // To use JUnit4, comment out JUnit5 and uncomment JUnit4 dependencies here. Please also check "test" section
    //implementation group: 'junit', name: 'junit', version: '4.13.1'
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.8.1'
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.1'

    implementation group: 'com.github.UnitTestBot.ksmt', name: 'ksmt-core', version: ksmtVersion
    implementation group: 'com.github.UnitTestBot.ksmt', name: 'ksmt-z3', version: ksmtVersion

    fetchInstrumentationJar project(path: ':utbot-instrumentation', configuration: 'instrumentationArchive')

    if (projectType == springProjectTypeName) {
        implementation project(':utbot-spring-commons-api')
        implementation project(':utbot-spring-analyzer')
        fetchSpringAnalyzerJar project(path: ':utbot-spring-analyzer', configuration: 'springAnalyzerJar')
    }
}

processResources {
    from(configurations.fetchInstrumentationJar) {
        into "lib"
    }

    if (projectType == springProjectTypeName) {
        from(configurations.fetchSpringAnalyzerJar) {
            into "lib"
        }
    }
}
