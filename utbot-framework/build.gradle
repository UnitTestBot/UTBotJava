repositories {
    flatDir {
        dirs 'dist'
    }
}

configurations {
    z3native
    fetchInstrumentationJar
}

dependencies {

    api project(':utbot-fuzzers')
    api project(':utbot-instrumentation')
    api project(':utbot-summary')
    api project(':utbot-framework-api')
    api project(':utbot-rd')

    implementation group: 'com.jetbrains.rd', name: 'rd-framework', version: rdVersion
    implementation group: 'com.jetbrains.rd', name: 'rd-core', version: rdVersion

    implementation("org.unittestbot.soot:soot-utbot-fork:${sootVersion}") {
        exclude group:'com.google.guava', module:'guava'
    }
    implementation group: 'com.google.guava', name: 'guava', version: guavaVersion
    implementation group: 'com.esotericsoftware.kryo', name: 'kryo5', version: kryoVersion
    // this is necessary for serialization of some collections
    implementation group: 'de.javakaffee', name: 'kryo-serializers', version: kryoSerializersVersion

    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: jacksonVersion
    implementation group: 'org.sosy-lab', name: 'javasmt-solver-z3', version: javasmtSolverZ3Version
    implementation group: 'com.github.curious-odd-man', name: 'rgxgen', version: rgxgenVersion
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j2Version
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlinLoggingVersion
    implementation group: 'org.jacoco', name: 'org.jacoco.report', version: jacocoVersion
    implementation group: 'org.apache.commons', name: 'commons-text', version: apacheCommonsTextVersion
    implementation "org.javaruntype:javaruntype:1.3"
    implementation "ru.vyarus:generics-resolver:3.0.3"
    implementation "ognl:ognl:3.3.2"

    // we need this for construction mocks from composite models
    implementation group: 'org.mockito', name: 'mockito-core', version: '4.2.0'

    // To use JUnit4, comment out JUnit5 and uncomment JUnit4 dependencies here. Please also check "test" section
    //implementation group: 'junit', name: 'junit', version: '4.13.1'
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.8.1'
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.1'

    z3native group: 'com.microsoft.z3', name: 'z3-native-win64', version: z3Version, ext: 'zip'
    z3native group: 'com.microsoft.z3', name: 'z3-native-linux64', version: z3Version, ext: 'zip'
    z3native group: 'com.microsoft.z3', name: 'z3-native-osx', version: z3Version, ext: 'zip'

    //For my tests
    fetchInstrumentationJar project(path: ':utbot-instrumentation', configuration:'instrumentationArchive')
}

processResources {
    configurations.z3native.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        from(zipTree(artifact.getFile())) {
            into "lib/x64"
        }
    }
    from(configurations.fetchInstrumentationJar) {
        into "lib"
    }
}
