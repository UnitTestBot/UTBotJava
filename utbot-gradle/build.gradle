plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.18.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply from: "${parent.projectDir}/gradle/include/jvm-project.gradle"

dependencies {
    shadow gradleApi()
    shadow localGroovy()

    implementation project(":utbot-framework")

    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlin_logging_version
}

// needed to prevent inclusion of gradle-api into shadow JAR
configurations.compile.dependencies.remove dependencies.gradleApi()

configurations.all {
    exclude group: "org.apache.logging.log4j", module: "log4j-slf4j-impl"
}

jar {
    manifest {
        // 'Fat JAR' is needed in org.utbot.framework.codegen.model.util.DependencyUtilsKt.checkDependencyIsFatJar
        attributes 'JAR-Type': 'Fat JAR'
        attributes 'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
    }
}

shadowJar {
    archiveClassifier.set('')
    minimize()
}

// no module metadata => no dependency on the `utbot-framework`
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            pom.withXml {
                // removing a dependency to `utbot-framework` from the list of dependencies
                asNode().dependencies.dependency.each { dependency ->
                    if (dependency.artifactId[0].value()[0] == 'utbot-framework') {
                        assert dependency.parent().remove(dependency)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('repo')
        }
    }
}

pluginBundle {
    website = 'https://www.utbot.org/'
    vcsUrl = 'https://github.com/UnitTestBot/UTBotJava/'
    tags = ['java', 'unit-testing', 'tests-generation', 'sarif']
}

gradlePlugin {
    plugins {
        sarifReportPlugin {
            version = '1.0.0-alpha-9' // last published version
            id = 'org.utbot.gradle.plugin'
            displayName = 'UnitTestBot gradle plugin'
            description = 'The gradle plugin for generating tests and creating SARIF reports based on UnitTestBot'
            implementationClass = 'org.utbot.gradle.plugin.SarifGradlePlugin'
        }
    }
}