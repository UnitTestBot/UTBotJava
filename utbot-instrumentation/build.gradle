apply from: "${parent.projectDir}/gradle/include/jvm-project.gradle"

dependencies {
    api project(':utbot-framework-api')

    implementation group: 'org.ow2.asm', name: 'asm', version: asm_version
    implementation group: 'org.ow2.asm', name: 'asm-commons', version: asm_version
    implementation group: 'com.esotericsoftware.kryo', name: 'kryo5', version: kryo_version
    // this is necessary for serialization of some collections
    implementation group: 'de.javakaffee', name: 'kryo-serializers', version: kryo_serializers_version
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlin_logging_version

    // TODO: this is necessary for inline classes mocking in UtExecutionInstrumentation
    implementation group: 'org.mockito', name: 'mockito-core', version: '4.2.0'
    implementation group: 'org.mockito', name: 'mockito-inline', version: '4.2.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin {
    dependsOn project(':utbot-api').tasks.jar
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += ["-Xallow-result-return-type", "-Xsam-conversions=class"]
        allWarningsAsErrors = false
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from sourceSets.main.output

    manifest {
        attributes (
                'Main-Class': 'org.utbot.instrumentation.process.ChildProcessKt',
                'Premain-Class': 'org.utbot.instrumentation.agent.Agent',
        )
    }

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

//noinspection GroovyAssignabilityCheck
configurations {
    instrumentationArchive
}

artifacts {
    instrumentationArchive jar
}
