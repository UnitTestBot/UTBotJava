compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation project(':utbot-framework-api')
    implementation project(':utbot-rd')

    implementation group: 'org.ow2.asm', name: 'asm', version: asmVersion
    implementation group: 'org.ow2.asm', name: 'asm-commons', version: asmVersion
    implementation group: 'com.esotericsoftware.kryo', name: 'kryo5', version: kryoVersion
    // this is necessary for serialization of some collections
    implementation group: 'de.javakaffee', name: 'kryo-serializers', version: kryoSerializersVersion
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlinLoggingVersion

    implementation group: 'com.jetbrains.rd', name: 'rd-framework', version: rdVersion
    implementation group: 'com.jetbrains.rd', name: 'rd-core', version: rdVersion
    implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.5.0'


    // TODO: this is necessary for inline classes mocking in UtExecutionInstrumentation
    implementation group: 'org.mockito', name: 'mockito-core', version: '4.2.0'
    implementation group: 'org.mockito', name: 'mockito-inline', version: '4.2.0'
}

java {
    withSourcesJar()
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes (
                'Main-Class': 'org.utbot.instrumentation.process.InstrumentedProcessMainKt',
                'Premain-Class': 'org.utbot.instrumentation.agent.Agent',
        )
    }

    // we need only classes from implementation and utbot to execute instrumented process
    dependsOn configurations.compileClasspath
    from {
        configurations.compileClasspath
                .findAll { it.isDirectory() || it.name.endsWith('jar') }
                .collect { it.isDirectory() ? it : zipTree(it) }
    }
}

configurations {
    instrumentationArchive
}

artifacts {
    instrumentationArchive jar
}