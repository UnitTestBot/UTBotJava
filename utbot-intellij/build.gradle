apply from: "${parent.projectDir}/gradle/include/jvm-project.gradle"

// TODO remove when switch plugin baseline to 2020.3 or higher, with kotlin 1.4 inside
compileKotlin {
    kotlinOptions {
        allWarningsAsErrors = false
    }
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath group: 'org.jetbrains.intellij.plugins', name: 'gradle-intellij-plugin', version: intellij_plugin_version
    }
}

dependencies {
    api project(':utbot-summary')
    api project(':utbot-analytics')
    api ('com.esotericsoftware:kryo:5.1.1')

    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlin_logging_version
    implementation group: 'org.apache.commons', name: 'commons-text', version: apache_commons_text_version
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: jackson_version

    implementation(project(":utbot-framework")) { exclude group: 'org.slf4j', module: 'slf4j-api' }
    implementation(project(":utbot-fuzzers"))

    testImplementation 'org.mock-server:mockserver-netty:5.4.1'
    testImplementation(project(":utbot-sample"))
    testApi(project(":utbot-framework"))
}

apply plugin: 'org.jetbrains.intellij'

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2020.2'
    // to use local IDEA comment out "version" and add localPath instead:
    // localPath 'c:/all/tools/idea'
    plugins = [
            'java',
            // TODO: SAT-1539 - specify version of android plugin to be supported by our kotlin version.
            "org.jetbrains.kotlin:${kotlin_version}-release-IJ2020.2-1",
            'org.jetbrains.android'
    ]
    updateSinceUntilBuild false

    patchPluginXml {
        sinceBuild = '202'
        version = project.version
    }
}

tasks {
    runIde {
        jvmArgs("-Xmx2048m")
        //// Uncomment and set correct path to Android Studio to run it
        // ideDirectory = file('d:/AS2021')
    }
}

publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/UnitTestBot/UTBotJava"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}

